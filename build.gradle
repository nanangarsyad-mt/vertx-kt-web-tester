buildscript {
  ext {
    kotlin_version = '1.3.31'
    vertx_version = '3.7.1'
  }

  repositories {
    jcenter()
//    maven {
//      url "https://oss.sonatype.org/content/repositories/snapshots/"
//    }
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

plugins {
  id 'java'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '5.0.0'
}

apply plugin: 'kotlin'

sourceCompatibility = '1.8'

mainClassName = 'io.vertx.core.Launcher'
def mainVerticleName = 'io.vertx.example.MainVerticle'

repositories {
  jcenter()
  maven {
    url "https://oss.sonatype.org/content/repositories/3963/"
  }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
  kotlinOptions {
    jvmTarget = "1.8"
  }
}

dependencies {
  compile "io.vertx:vertx-core:$vertx_version"
  compile "io.vertx:vertx-web:$vertx_version"

  compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
  compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.1"
}

// Redeploy watcher.
run {
  args = ['run', mainVerticleName,
          "--launcher-class=$mainClassName",
          "--redeploy=src/**/*.*",
          "--on-redeploy=./gradlew classes"
  ]
}

// Naming and packaging settings for the "shadow jar".
shadowJar {
  baseName = 'app'
  classifier = 'shadow'

  manifest {
    attributes 'Main-Verticle': mainVerticleName
  }
  mergeServiceFiles {
    include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
  }
}

// Heroku relies on the 'stage' task to deploy.
task stage {
  dependsOn shadowJar
}
